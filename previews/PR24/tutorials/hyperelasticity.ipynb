{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Nonlinear Elasticity\n",
    "\n",
    "\n",
    "\n",
    "## Implementation"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "The following code is based on the [Hyperelasticity](https://ferrite-fem.github.io/Ferrite.jl/stable/tutorials/hyperelasticity/) tutorial from the Ferrite.jl documentation, with some comments removed for brevity.\n",
    "There are two main modifications:\n",
    "\n",
    "1. Second-order `Lagrange` shape functions are used for field approximation: `ip = Lagrange{RefTriangle,2}()^2`.\n",
    "2. Four quadrature points are used to accommodate the second-order shape functions: `qr = QuadratureRule{RefTriangle}(4)`."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Ferrite, Tensors, TimerOutputs, IterativeSolvers\n",
    "\n",
    "using FerriteMultigrid\n",
    "\n",
    "TimerOutputs.enable_debug_timings(AlgebraicMultigrid)\n",
    "TimerOutputs.enable_debug_timings(FerriteMultigrid)\n",
    "\n",
    "struct NeoHooke\n",
    "    μ::Float64\n",
    "    λ::Float64\n",
    "end\n",
    "\n",
    "function Ψ(C, mp::NeoHooke)\n",
    "    μ = mp.μ\n",
    "    λ = mp.λ\n",
    "    Ic = tr(C)\n",
    "    J = sqrt(det(C))\n",
    "    return μ / 2 * (Ic - 3 - 2 * log(J)) + λ / 2 * (J - 1)^2\n",
    "end\n",
    "\n",
    "function constitutive_driver(C, mp::NeoHooke)\n",
    "    # Compute all derivatives in one function call\n",
    "    ∂²Ψ∂C², ∂Ψ∂C = Tensors.hessian(y -> Ψ(y, mp), C, :all)\n",
    "    S = 2.0 * ∂Ψ∂C\n",
    "    ∂S∂C = 2.0 * ∂²Ψ∂C²\n",
    "    return S, ∂S∂C\n",
    "end;\n",
    "\n",
    "function assemble_element!(ke, ge, cell, cv, fv, mp, ue, ΓN)\n",
    "    # Reinitialize cell values, and reset output arrays\n",
    "    reinit!(cv, cell)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(ge, 0.0)\n",
    "\n",
    "    b = Vec{3}((0.0, -0.5, 0.0)) # Body force\n",
    "    tn = 0.1 # Traction (to be scaled with surface normal)\n",
    "    ndofs = getnbasefunctions(cv)\n",
    "\n",
    "    for qp in 1:getnquadpoints(cv)\n",
    "        dΩ = getdetJdV(cv, qp)\n",
    "        # Compute deformation gradient F and right Cauchy-Green tensor C\n",
    "        ∇u = function_gradient(cv, qp, ue)\n",
    "        F = one(∇u) + ∇u\n",
    "        C = tdot(F) # F' ⋅ F\n",
    "        # Compute stress and tangent\n",
    "        S, ∂S∂C = constitutive_driver(C, mp)\n",
    "        P = F ⋅ S\n",
    "        I = one(S)\n",
    "        ∂P∂F = otimesu(I, S) + 2 * F ⋅ ∂S∂C ⊡ otimesu(F', I)\n",
    "\n",
    "        # Loop over test functions\n",
    "        for i in 1:ndofs\n",
    "            # Test function and gradient\n",
    "            δui = shape_value(cv, qp, i)\n",
    "            ∇δui = shape_gradient(cv, qp, i)\n",
    "            # Add contribution to the residual from this test function\n",
    "            ge[i] += (∇δui ⊡ P - δui ⋅ b) * dΩ\n",
    "\n",
    "            ∇δui∂P∂F = ∇δui ⊡ ∂P∂F # Hoisted computation\n",
    "            for j in 1:ndofs\n",
    "                ∇δuj = shape_gradient(cv, qp, j)\n",
    "                # Add contribution to the tangent\n",
    "                ke[i, j] += (∇δui∂P∂F ⊡ ∇δuj) * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Surface integral for the traction\n",
    "    for facet in 1:nfacets(cell)\n",
    "        if (cellid(cell), facet) in ΓN\n",
    "            reinit!(fv, cell, facet)\n",
    "            for q_point in 1:getnquadpoints(fv)\n",
    "                t = tn * getnormal(fv, q_point)\n",
    "                dΓ = getdetJdV(fv, q_point)\n",
    "                for i in 1:ndofs\n",
    "                    δui = shape_value(fv, q_point, i)\n",
    "                    ge[i] -= (δui ⋅ t) * dΓ\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return\n",
    "end;\n",
    "\n",
    "function assemble_global!(K, g, dh, cv, fv, mp, u, ΓN)\n",
    "    n = ndofs_per_cell(dh)\n",
    "    ke = zeros(n, n)\n",
    "    ge = zeros(n)\n",
    "\n",
    "    # start_assemble resets K and g\n",
    "    assembler = start_assemble(K, g)\n",
    "\n",
    "    # Loop over all cells in the grid\n",
    "    for cell in CellIterator(dh)\n",
    "        global_dofs = celldofs(cell)\n",
    "        ue = u[global_dofs] # element dofs\n",
    "        assemble_element!(ke, ge, cell, cv, fv, mp, ue, ΓN)\n",
    "        assemble!(assembler, global_dofs, ke, ge)\n",
    "    end\n",
    "    return\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Near Null Space (NNS)\n",
    "\n",
    "In multigrid methods for problems with vector-valued unknowns, such as elasticity,\n",
    "the near null space represents the low energy mode or the smooth error that needs to be captured\n",
    "in the coarser grid when using SA-AMG (Smoothed Aggregation Algebraic Multigrid), more on the topic\n",
    "can be found  in [schroder2010](@citet)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "For 3D linear elasticity problems, the rigid body modes are:\n",
    "1. Translation in the x-direction,\n",
    "2. Translation in the y-direction,\n",
    "3. Translation in the z-direction,\n",
    "4. Rotation about the x-axis (i.e., $x_1$): each point (x, y, z) is mapped to (0, -z, y).\n",
    "5. Rotation about the y-axis (i.e., $x_2$): each point (x, y, z) is mapped to (z, 0, -x).\n",
    "6. Rotation about the z-axis (i.e., $x_3$): each point (x, y, z) is mapped to (-y, x, 0).\n",
    "\n",
    "The function `create_nns` constructs the NNS matrix `B ∈ ℝ^{n × 6}`, where `n` is the number of degrees of freedom (DOFs)\n",
    "for the case of `p` = 1 (i.e., linear interpolation), because `B` is only relevant for AMG."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[ Info: 27783\n",
      "---------------------------------------------------------------------------------------\n",
      "     Analysis with 6000 elements              Time                    Allocations      \n",
      "                                     -----------------------   ------------------------\n",
      "          Tot / % measured:               14.3s /  85.5%           1.17GiB /  85.2%    \n",
      "\n",
      "Section                      ncalls     time    %tot     avg     alloc    %tot      avg\n",
      "---------------------------------------------------------------------------------------\n",
      "Setup preconditioner              1    4.70s   38.4%   4.70s    715MiB   69.8%   715MiB\n",
      "  pmultigrid hierarchy            1    4.69s   38.4%   4.69s    715MiB   69.8%   715MiB\n",
      "    coarse solver setup           1    2.12s   17.4%   2.12s    327MiB   31.9%   327MiB\n",
      "      coarse solver setup         1    603ms    4.9%   603ms   41.3MiB    4.0%  41.3MiB\n",
      "      extend_hierarchy!           3   95.2ms    0.8%  31.7ms   78.5MiB    7.7%  26.2MiB\n",
      "        restriction setup         3   45.6ms    0.4%  15.2ms   38.6MiB    3.8%  12.9MiB\n",
      "        RAP                       3   32.6ms    0.3%  10.9ms   33.7MiB    3.3%  11.2MiB\n",
      "        improve candidates        3   10.4ms    0.1%  3.46ms     0.00B    0.0%    0.00B\n",
      "        fit candidates            3   4.39ms    0.0%  1.46ms   2.79MiB    0.3%   952KiB\n",
      "        strength                  3   2.12ms    0.0%   708μs   2.99MiB    0.3%  1.00MiB\n",
      "        aggregation               3   92.6μs    0.0%  30.9μs    144KiB    0.0%  47.9KiB\n",
      "      ml setup                    1   6.07ms    0.0%  6.07ms   2.67KiB    0.0%  2.67KiB\n",
      "      prologue                    1   53.5μs    0.0%  53.5μs    219KiB    0.0%   219KiB\n",
      "    extend_hierarchy!             1    1.93s   15.7%   1.93s    318MiB   31.1%   318MiB\n",
      "      build prolongator           1    999ms    8.2%   999ms    156MiB   15.2%   156MiB\n",
      "        assembly                  1    999ms    8.2%   999ms    156MiB   15.2%   156MiB\n",
      "        row normalization         1   81.8μs    0.0%  81.8μs     0.00B    0.0%    0.00B\n",
      "      RAP                         1   63.4ms    0.5%  63.4ms   88.6MiB    8.6%  88.6MiB\n",
      "      build restriction           1    391ns    0.0%   391ns     48.0B    0.0%    48.0B\n",
      "    coarsen order                 1    489ms    4.0%   489ms   63.9MiB    6.2%  63.9MiB\n",
      "export                            1    2.40s   19.6%   2.40s    158MiB   15.5%   158MiB\n",
      "GMRES                             1    1.76s   14.4%   1.76s   4.68MiB    0.5%  4.68MiB\n",
      "Galerkin CG                       1    1.36s   11.1%   1.36s   42.2MiB    4.1%  42.2MiB\n",
      "  Postsmoother                   19    135ms    1.1%  7.11ms     0.00B    0.0%    0.00B\n",
      "  Presmoother                    19    131ms    1.1%  6.89ms     0.00B    0.0%    0.00B\n",
      "  Residual eval                  19   27.5ms    0.2%  1.45ms     0.00B    0.0%    0.00B\n",
      "  Coarse solve                   19   23.4ms    0.2%  1.23ms    684KiB    0.1%  36.0KiB\n",
      "    Presmoother                  57   8.72ms    0.1%   153μs     0.00B    0.0%    0.00B\n",
      "    Postsmoother                 57   8.17ms    0.1%   143μs     0.00B    0.0%    0.00B\n",
      "    Residual eval                57   2.92ms    0.0%  51.3μs     0.00B    0.0%    0.00B\n",
      "    Restriction                  57   1.51ms    0.0%  26.6μs     0.00B    0.0%    0.00B\n",
      "    Prolongation                 57   1.24ms    0.0%  21.7μs     0.00B    0.0%    0.00B\n",
      "    Coarse solve                 19   51.8μs    0.0%  2.72μs     0.00B    0.0%    0.00B\n",
      "  Prolongation                   19    976μs    0.0%  51.4μs     0.00B    0.0%    0.00B\n",
      "  Restriction                    19    828μs    0.0%  43.6μs     0.00B    0.0%    0.00B\n",
      "Galerkin GMRES                    1    1.14s    9.3%   1.14s    104MiB   10.1%   104MiB\n",
      "  Postsmoother                   19    135ms    1.1%  7.10ms     0.00B    0.0%    0.00B\n",
      "  Presmoother                    19    133ms    1.1%  6.98ms     0.00B    0.0%    0.00B\n",
      "  Residual eval                  19   27.7ms    0.2%  1.46ms     0.00B    0.0%    0.00B\n",
      "  Coarse solve                   19   23.3ms    0.2%  1.23ms    684KiB    0.1%  36.0KiB\n",
      "    Presmoother                  57   8.64ms    0.1%   152μs     0.00B    0.0%    0.00B\n",
      "    Postsmoother                 57   8.15ms    0.1%   143μs     0.00B    0.0%    0.00B\n",
      "    Residual eval                57   2.95ms    0.0%  51.7μs     0.00B    0.0%    0.00B\n",
      "    Restriction                  57   1.53ms    0.0%  26.9μs     0.00B    0.0%    0.00B\n",
      "    Prolongation                 57   1.25ms    0.0%  22.0μs     0.00B    0.0%    0.00B\n",
      "    Coarse solve                 19   40.2μs    0.0%  2.12μs     0.00B    0.0%    0.00B\n",
      "  Prolongation                   19    977μs    0.0%  51.4μs     0.00B    0.0%    0.00B\n",
      "  Restriction                    19    880μs    0.0%  46.3μs     0.00B    0.0%    0.00B\n",
      "CG                                1    881ms    7.2%   881ms    652KiB    0.1%   652KiB\n",
      "---------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "function create_nns(dh, fieldname = first(dh.field_names))\n",
    "    @assert length(dh.field_names) == 1 \"Only a single field is supported for now.\"\n",
    "\n",
    "    coords_flat = zeros(ndofs(dh))\n",
    "    apply_analytical!(coords_flat, dh, fieldname, x -> x)\n",
    "    coords = reshape(coords_flat, (length(coords_flat) ÷ 3, 3))\n",
    "\n",
    "    grid = dh.grid\n",
    "    B = zeros(Float64, ndofs(dh), 6)\n",
    "    B[1:3:end, 1] .= 1 # x - translation\n",
    "    B[2:3:end, 2] .= 1 # y - translation\n",
    "    B[3:3:end, 3] .= 1 # z - translation\n",
    "\n",
    "    # rotations\n",
    "    x = coords[:, 1]\n",
    "    y = coords[:, 2]\n",
    "    z = coords[:, 3]\n",
    "    # Around x\n",
    "    B[2:3:end, 4] .= -z\n",
    "    B[3:3:end, 4] .= y\n",
    "    # Around y\n",
    "    B[1:3:end, 5] .= z\n",
    "    B[3:3:end, 5] .= -x\n",
    "    # Around z\n",
    "    B[1:3:end, 6] .= -y\n",
    "    B[2:3:end, 6] .= x\n",
    "\n",
    "    return B\n",
    "end\n",
    "\n",
    "function _solve(N = 10)\n",
    "    reset_timer!()\n",
    "\n",
    "    # Generate a grid\n",
    "    L = 1.0\n",
    "    left = zero(Vec{3})\n",
    "    right = L * ones(Vec{3})\n",
    "    grid = generate_grid(Tetrahedron, (N, N, N), left, right)\n",
    "\n",
    "    # Material parameters\n",
    "    E = 10.0\n",
    "    ν = 0.3\n",
    "    μ = E / (2(1 + ν))\n",
    "    λ = (E * ν) / ((1 + ν) * (1 - 2ν))\n",
    "    mp = NeoHooke(μ, λ)\n",
    "\n",
    "    # Finite element base\n",
    "    ip = Lagrange{RefTetrahedron, 2}()^3\n",
    "    qr = QuadratureRule{RefTetrahedron}(4)\n",
    "    qr_facet = FacetQuadratureRule{RefTetrahedron}(3)\n",
    "    cv = CellValues(qr, ip)\n",
    "    fv = FacetValues(qr_facet, ip)\n",
    "\n",
    "    # DofHandler\n",
    "    dh = DofHandler(grid)\n",
    "    add!(dh, :u, ip) # Add a displacement field\n",
    "    close!(dh)\n",
    "\n",
    "    function rotation(X, t)\n",
    "        θ = pi / 3 # 60°\n",
    "        x, y, z = X\n",
    "        return t * Vec{3}(\n",
    "            (\n",
    "                0.0,\n",
    "                L / 2 - y + (y - L / 2) * cos(θ) - (z - L / 2) * sin(θ),\n",
    "                L / 2 - z + (y - L / 2) * sin(θ) + (z - L / 2) * cos(θ),\n",
    "            )\n",
    "        )\n",
    "    end\n",
    "\n",
    "    ch = ConstraintHandler(dh)\n",
    "    # Add a homogeneous boundary condition on the \"clamped\" edge\n",
    "    dbc = Dirichlet(:u, getfacetset(grid, \"right\"), (x, t) -> [0.0, 0.0, 0.0], [1, 2, 3])\n",
    "    add!(ch, dbc)\n",
    "    dbc = Dirichlet(:u, getfacetset(grid, \"left\"), (x, t) -> rotation(x, t), [1, 2, 3])\n",
    "    add!(ch, dbc)\n",
    "    close!(ch)\n",
    "    t = 0.5\n",
    "    Ferrite.update!(ch, t)\n",
    "\n",
    "    # Neumann part of the boundary\n",
    "    ΓN = union(\n",
    "        getfacetset(grid, \"top\"),\n",
    "        getfacetset(grid, \"bottom\"),\n",
    "        getfacetset(grid, \"front\"),\n",
    "        getfacetset(grid, \"back\"),\n",
    "    )\n",
    "\n",
    "    # Pre-allocation of vectors for the solution and Newton increments\n",
    "    _ndofs = ndofs(dh)\n",
    "    un = zeros(_ndofs) # previous solution vector\n",
    "    u = zeros(_ndofs)\n",
    "    Δu = zeros(_ndofs)\n",
    "    ΔΔu = zeros(_ndofs)\n",
    "    apply!(un, ch)\n",
    "\n",
    "    # Create sparse matrix and residual vector\n",
    "    K = allocate_matrix(dh)\n",
    "    g = zeros(_ndofs)\n",
    "\n",
    "    # FIXME this needs better integration\n",
    "    dh_coarse = DofHandler(grid)\n",
    "    add!(dh_coarse, :u, Lagrange{RefTetrahedron, 1}()^3) # Add a displacement field\n",
    "    close!(dh_coarse)\n",
    "    B = create_nns(dh_coarse)\n",
    "    config_gal = pmultigrid_config(coarse_strategy = Galerkin())\n",
    "    fe_space = FESpace(dh, cv, ch)\n",
    "    pcoarse_solver = SmoothedAggregationCoarseSolver(; B)\n",
    "    builder = PMultigridPreconBuilder(fe_space, config_gal; pcoarse_solver)\n",
    "\n",
    "    # Perform Newton iterations\n",
    "    newton_itr = -1\n",
    "    NEWTON_TOL = 1.0e-8\n",
    "    NEWTON_MAXITER = 30\n",
    "\n",
    "    @info ndofs(dh)\n",
    "\n",
    "    while true\n",
    "        newton_itr += 1\n",
    "        # Construct the current guess\n",
    "        u .= un .+ Δu\n",
    "        # Compute residual and tangent for current guess\n",
    "        assemble_global!(K, g, dh, cv, fv, mp, u, ΓN)\n",
    "        # Apply boundary conditions\n",
    "        apply_zero!(K, g, ch)\n",
    "        # Compute the residual norm and compare with tolerance\n",
    "        normg = norm(g)\n",
    "        if normg < NEWTON_TOL\n",
    "            break\n",
    "        elseif newton_itr > NEWTON_MAXITER\n",
    "            error(\"Reached maximum Newton iterations, aborting\")\n",
    "        end\n",
    "\n",
    "        # Compute increment using conjugate gradients\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"Setup preconditioner\" Pl = builder(K)[1]\n",
    "        @timeit \"Galerkin CG\" IterativeSolvers.cg!(ΔΔu, K, g; Pl, maxiter = 100, verbose=false)\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"Galerkin GMRES\" IterativeSolvers.gmres!(ΔΔu, K, g; Pl, maxiter = 100, verbose=false)\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"CG\" IterativeSolvers.cg!(ΔΔu, K, g; maxiter = 1000, verbose=false)\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"GMRES\" IterativeSolvers.gmres!(ΔΔu, K, g; maxiter = 1000, verbose=false)\n",
    "\n",
    "        apply_zero!(ΔΔu, ch)\n",
    "        Δu .-= ΔΔu\n",
    "        break\n",
    "    end\n",
    "\n",
    "    # Save the solution\n",
    "    @timeit \"export\" begin\n",
    "        VTKGridFile(\"hyperelasticity\", dh) do vtk\n",
    "            write_solution(vtk, dh, u)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    print_timer(title = \"Analysis with $(getncells(grid)) elements\", linechars = :ascii)\n",
    "    return u\n",
    "end\n",
    "\n",
    "u = _solve();"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.5"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.5",
   "language": "julia"
  }
 },
 "nbformat": 4
}