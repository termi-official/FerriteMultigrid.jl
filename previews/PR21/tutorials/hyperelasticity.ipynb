{
 "cells": [
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[ Info: 27783\n",
      "---------------------------------------------------------------------------------\n",
      "  Analysis with 6000 elements           Time                    Allocations      \n",
      "                               -----------------------   ------------------------\n",
      "       Tot / % measured:            63.1s /  97.6%           20.4GiB /  99.3%    \n",
      "\n",
      "Section                ncalls     time    %tot     avg     alloc    %tot      avg\n",
      "---------------------------------------------------------------------------------\n",
      "Galerkin only               5    30.7s   49.8%   6.14s   16.6GiB   82.1%  3.33GiB\n",
      "Setup preconditioner        5    12.3s   20.0%   2.46s    901MiB    4.3%   180MiB\n",
      "CG                          5    11.4s   18.4%   2.27s   2.58GiB   12.8%   529MiB\n",
      "Galerkin CG                 5    2.97s    4.8%   595ms   3.18MiB    0.0%   652KiB\n",
      "export                      1    2.62s    4.3%   2.62s    159MiB    0.8%   159MiB\n",
      "assemble                    6    1.64s    2.7%   274ms   10.4MiB    0.1%  1.74MiB\n",
      "  element assemble      36.0k    1.44s    2.3%  39.9μs     0.00B    0.0%    0.00B\n",
      "---------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "using Ferrite, Tensors, TimerOutputs, IterativeSolvers\n",
    "\n",
    "using FerriteMultigrid\n",
    "\n",
    "struct NeoHooke\n",
    "    μ::Float64\n",
    "    λ::Float64\n",
    "end\n",
    "\n",
    "function Ψ(C, mp::NeoHooke)\n",
    "    μ = mp.μ\n",
    "    λ = mp.λ\n",
    "    Ic = tr(C)\n",
    "    J = sqrt(det(C))\n",
    "    return μ / 2 * (Ic - 3 - 2 * log(J)) + λ / 2 * (J - 1)^2\n",
    "end\n",
    "\n",
    "function constitutive_driver(C, mp::NeoHooke)\n",
    "    # Compute all derivatives in one function call\n",
    "    ∂²Ψ∂C², ∂Ψ∂C = Tensors.hessian(y -> Ψ(y, mp), C, :all)\n",
    "    S = 2.0 * ∂Ψ∂C\n",
    "    ∂S∂C = 2.0 * ∂²Ψ∂C²\n",
    "    return S, ∂S∂C\n",
    "end;\n",
    "\n",
    "function assemble_element!(ke, ge, cell, cv, fv, mp, ue, ΓN)\n",
    "    # Reinitialize cell values, and reset output arrays\n",
    "    reinit!(cv, cell)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(ge, 0.0)\n",
    "\n",
    "    b = Vec{3}((0.0, -0.5, 0.0)) # Body force\n",
    "    tn = 0.1 # Traction (to be scaled with surface normal)\n",
    "    ndofs = getnbasefunctions(cv)\n",
    "\n",
    "    for qp in 1:getnquadpoints(cv)\n",
    "        dΩ = getdetJdV(cv, qp)\n",
    "        # Compute deformation gradient F and right Cauchy-Green tensor C\n",
    "        ∇u = function_gradient(cv, qp, ue)\n",
    "        F = one(∇u) + ∇u\n",
    "        C = tdot(F) # F' ⋅ F\n",
    "        # Compute stress and tangent\n",
    "        S, ∂S∂C = constitutive_driver(C, mp)\n",
    "        P = F ⋅ S\n",
    "        I = one(S)\n",
    "        ∂P∂F = otimesu(I, S) + 2 * F ⋅ ∂S∂C ⊡ otimesu(F', I)\n",
    "\n",
    "        # Loop over test functions\n",
    "        for i in 1:ndofs\n",
    "            # Test function and gradient\n",
    "            δui = shape_value(cv, qp, i)\n",
    "            ∇δui = shape_gradient(cv, qp, i)\n",
    "            # Add contribution to the residual from this test function\n",
    "            ge[i] += (∇δui ⊡ P - δui ⋅ b) * dΩ\n",
    "\n",
    "            ∇δui∂P∂F = ∇δui ⊡ ∂P∂F # Hoisted computation\n",
    "            for j in 1:ndofs\n",
    "                ∇δuj = shape_gradient(cv, qp, j)\n",
    "                # Add contribution to the tangent\n",
    "                ke[i, j] += (∇δui∂P∂F ⊡ ∇δuj) * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Surface integral for the traction\n",
    "    for facet in 1:nfacets(cell)\n",
    "        if (cellid(cell), facet) in ΓN\n",
    "            reinit!(fv, cell, facet)\n",
    "            for q_point in 1:getnquadpoints(fv)\n",
    "                t = tn * getnormal(fv, q_point)\n",
    "                dΓ = getdetJdV(fv, q_point)\n",
    "                for i in 1:ndofs\n",
    "                    δui = shape_value(fv, q_point, i)\n",
    "                    ge[i] -= (δui ⋅ t) * dΓ\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return\n",
    "end;\n",
    "\n",
    "function assemble_global!(K, g, dh, cv, fv, mp, u, ΓN)\n",
    "    n = ndofs_per_cell(dh)\n",
    "    ke = zeros(n, n)\n",
    "    ge = zeros(n)\n",
    "\n",
    "    # start_assemble resets K and g\n",
    "    assembler = start_assemble(K, g)\n",
    "\n",
    "    # Loop over all cells in the grid\n",
    "    @timeit \"assemble\" for cell in CellIterator(dh)\n",
    "        global_dofs = celldofs(cell)\n",
    "        ue = u[global_dofs] # element dofs\n",
    "        @timeit \"element assemble\" assemble_element!(ke, ge, cell, cv, fv, mp, ue, ΓN)\n",
    "        assemble!(assembler, global_dofs, ke, ge)\n",
    "    end\n",
    "    return\n",
    "end;\n",
    "\n",
    "function create_nns(dh)\n",
    "    ##Ndof = ndofs(dh)\n",
    "    grid = dh.grid\n",
    "    Ndof = 3 * (grid.nodes |> length) # nns at p = 1 for AMG\n",
    "    B = zeros(Float64, Ndof, 6)\n",
    "    B[1:3:end, 1] .= 1 # x - translation\n",
    "    B[2:3:end, 2] .= 1 # y - translation\n",
    "    B[3:3:end, 3] .= 1 # z - translation\n",
    "\n",
    "    # rotations\n",
    "    coords = reduce(hcat, grid.nodes .|> (n -> n.x |> collect))' # convert nodes to 2d array\n",
    "    z = coords[:, 3]\n",
    "    y = coords[:, 2]\n",
    "    x = coords[:, 1]\n",
    "    B[2:3:end, 4] .= -z\n",
    "    B[3:3:end, 4] .= y\n",
    "    B[1:3:end, 5] .= z\n",
    "    B[3:3:end, 5] .= -x\n",
    "    B[1:3:end, 6] .= -y\n",
    "    B[3:3:end, 6] .= x\n",
    "    return B\n",
    "end\n",
    "\n",
    "function _solve()\n",
    "    reset_timer!()\n",
    "\n",
    "    # Generate a grid\n",
    "    N = 10\n",
    "    L = 1.0\n",
    "    left = zero(Vec{3})\n",
    "    right = L * ones(Vec{3})\n",
    "    grid = generate_grid(Tetrahedron, (N, N, N), left, right)\n",
    "\n",
    "    # Material parameters\n",
    "    E = 10.0\n",
    "    ν = 0.3\n",
    "    μ = E / (2(1 + ν))\n",
    "    λ = (E * ν) / ((1 + ν) * (1 - 2ν))\n",
    "    mp = NeoHooke(μ, λ)\n",
    "\n",
    "    # Finite element base\n",
    "    ip = Lagrange{RefTetrahedron, 2}()^3\n",
    "    qr = QuadratureRule{RefTetrahedron}(4)\n",
    "    qr_facet = FacetQuadratureRule{RefTetrahedron}(3)\n",
    "    cv = CellValues(qr, ip)\n",
    "    fv = FacetValues(qr_facet, ip)\n",
    "\n",
    "    # DofHandler\n",
    "    dh = DofHandler(grid)\n",
    "    add!(dh, :u, ip) # Add a displacement field\n",
    "    close!(dh)\n",
    "\n",
    "    function rotation(X, t)\n",
    "        θ = pi / 3 # 60°\n",
    "        x, y, z = X\n",
    "        return t * Vec{3}(\n",
    "            (\n",
    "                0.0,\n",
    "                L / 2 - y + (y - L / 2) * cos(θ) - (z - L / 2) * sin(θ),\n",
    "                L / 2 - z + (y - L / 2) * sin(θ) + (z - L / 2) * cos(θ),\n",
    "            )\n",
    "        )\n",
    "    end\n",
    "\n",
    "    ch = ConstraintHandler(dh)\n",
    "    # Add a homogeneous boundary condition on the \"clamped\" edge\n",
    "    dbc = Dirichlet(:u, getfacetset(grid, \"right\"), (x, t) -> [0.0, 0.0, 0.0], [1, 2, 3])\n",
    "    add!(ch, dbc)\n",
    "    dbc = Dirichlet(:u, getfacetset(grid, \"left\"), (x, t) -> rotation(x, t), [1, 2, 3])\n",
    "    add!(ch, dbc)\n",
    "    close!(ch)\n",
    "    t = 0.5\n",
    "    Ferrite.update!(ch, t)\n",
    "\n",
    "    # Neumann part of the boundary\n",
    "    ΓN = union(\n",
    "        getfacetset(grid, \"top\"),\n",
    "        getfacetset(grid, \"bottom\"),\n",
    "        getfacetset(grid, \"front\"),\n",
    "        getfacetset(grid, \"back\"),\n",
    "    )\n",
    "\n",
    "    # Pre-allocation of vectors for the solution and Newton increments\n",
    "    _ndofs = ndofs(dh)\n",
    "    un = zeros(_ndofs) # previous solution vector\n",
    "    u = zeros(_ndofs)\n",
    "    Δu = zeros(_ndofs)\n",
    "    ΔΔu = zeros(_ndofs)\n",
    "    apply!(un, ch)\n",
    "\n",
    "    # Create sparse matrix and residual vector\n",
    "    K = allocate_matrix(dh)\n",
    "    g = zeros(_ndofs)\n",
    "\n",
    "    B = create_nns(dh)\n",
    "    config_gal = pmultigrid_config(coarse_strategy = Galerkin())\n",
    "    fe_space = FESpace(dh, cv, ch)\n",
    "\n",
    "    builder = PMultigridPreconBuilder(fe_space, config_gal)\n",
    "\n",
    "    # Perform Newton iterations\n",
    "    newton_itr = -1\n",
    "    NEWTON_TOL = 1.0e-8\n",
    "    NEWTON_MAXITER = 30\n",
    "\n",
    "    @info ndofs(dh)\n",
    "\n",
    "    while true\n",
    "        newton_itr += 1\n",
    "        # Construct the current guess\n",
    "        u .= un .+ Δu\n",
    "        # Compute residual and tangent for current guess\n",
    "        assemble_global!(K, g, dh, cv, fv, mp, u, ΓN)\n",
    "        # Apply boundary conditions\n",
    "        apply_zero!(K, g, ch)\n",
    "        # Compute the residual norm and compare with tolerance\n",
    "        normg = norm(g)\n",
    "        if normg < NEWTON_TOL\n",
    "            break\n",
    "        elseif newton_itr > NEWTON_MAXITER\n",
    "            error(\"Reached maximum Newton iterations, aborting\")\n",
    "        end\n",
    "\n",
    "        # Compute increment using conjugate gradients\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"Setup preconditioner\" Pl = builder(K)[1]\n",
    "        @timeit \"CG\" IterativeSolvers.cg!(ΔΔu, K, g; Pl, maxiter = 1000, verbose=false)\n",
    "        @timeit \"Galerkin only\" solve(K, g, fe_space, config_gal;B = B, log=true, rtol = 1e-10)\n",
    "        fill!(ΔΔu, 0.0)\n",
    "        @timeit \"Galerkin CG\" IterativeSolvers.cg!(ΔΔu, K, g; maxiter = 1000, verbose=false)\n",
    "\n",
    "        apply_zero!(ΔΔu, ch)\n",
    "        Δu .-= ΔΔu\n",
    "    end\n",
    "\n",
    "    # Save the solution\n",
    "    @timeit \"export\" begin\n",
    "        VTKGridFile(\"hyperelasticity\", dh) do vtk\n",
    "            write_solution(vtk, dh, u)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    print_timer(title = \"Analysis with $(getncells(grid)) elements\", linechars = :ascii)\n",
    "    return u\n",
    "end\n",
    "\n",
    "u = _solve();"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}